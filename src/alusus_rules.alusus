@merge module Jameel {
    module AlususRules {
        // Because the standard libc regex lib doesn't support utf-8 we'll have to work around it by treating non-ascii
        // characters as strings, in other words avoid character groups or characters ranges with non-ascii chars.
        def identifierChar: String(
            "([a-zA-Z0-9_]|ا|ب|ت|ث|ج|ح|خ|د|ذ|ر|ز|س|ش|ص|ض|ط|ظ|ع|غ|ف|ق|ك|ل|م|ن|ه|و|ي|ـ|ى|ؤ|ئ|ء|أ|إ|آ|ّ|َ|ً|ُ|ٌ|ِ|ٍ|ْ)"
        );
        def nonIdentifierChar: String("[ \t\n\r\\(\\)\\{\\}+=!@#$%^&*\\[\\]|,/-]");
        def spacesThenIdentifier: Regex.Matcher(String("^[ \t]*") + identifierChar, 1);
        def spacesAndNewLines: Regex.Matcher("^[ \t\n\r]+", 1);
        def emptyLine: Regex.Matcher("^[ \t]*([\n\r])", 1);
        def nonEmptyLine: Regex.Matcher("^[ \t]*([^\n\r])", 1);
        def spacesAndNewLinesThenClosingParen: Regex.Matcher("^[ \t\n\r]*(\\)|\\])", 1);
        def spacesThenClosingBrace: Regex.Matcher("^[ \t]*\\}", 1);
        def closingBraceOnSameLine: Regex.Matcher("^[^\n\\{]*\\}", 1);
        def spacesAndNewLinesThenClosingBrace: Regex.Matcher("^[ \t\n\r]*\\}", 1);
        def spacesAndNewLinesThenOpeningBrace: Regex.Matcher("^[ \t\n\r]*\\{", 1);
        def spacesAndNewLinesThenElseKeyword: Regex.Matcher(String.format("^[ \t\n\r]*(else|وإلا)%s", nonIdentifierChar.buf), 1);
        def spacesAndNewLinesThenSemiColon: Regex.Matcher("^[ \t\n\r]*(;|؛)", 1);

        // TODO: Test bigger code samples.

        function getRules (): Array[SrdRef[Rule]] {
            def contextStack: SrdRef[Array[Context]];
            contextStack.construct();
            contextStack.add(Context());

            return Array[SrdRef[Rule]]({
                // Comment Lines
                Rule("(//[^\n]*)[ \t]*\n", closure(args: Array[String]) {
                    printSourceCode(args(1));
                    handleNewLines(contextStack, 1);
                }),
                // Comment Blocks
                Rule("([ \t]*/\\*)", closure(args: Array[String]) {
                    printSourceCode(args(1));
                    contextStack.add(Context(String("comment"), false));
                }),
                Rule("(\\*/)", closure(args: Array[String]) {
                    popContextStack(contextStack);
                    printSourceCode(args(1));
                    printSourceCode(" ");
                    handleNewLines(contextStack, 0);
                }),
                // String Literals
                Rule("\"([^\"]|\\\\\")*\"", closure(args: Array[String]) {
                    printSourceCode(args(0));
                }),
                // Colon
                Rule(":[ \t]*", closure(args: Array[String]) {
                    if getCurrentContext(contextStack) == "comment" printSourceCode(args(0))
                    else printSourceCode(": ");
                }),
                // Commas
                Rule("(,|،)[ \t]*", closure(args: Array[String]) {
                    if getCurrentContext(contextStack) == "comment" printSourceCode(args(0))
                    else if isNext(getCurrentContext(contextStack).closingMatcher) {
                        printSourceCode(args(1));
                        skip(spacesAndNewLines);
                    } else if getCurrentContent()~cnt(0) == '\n' {
                        printSourceCode(String.format("%s\n", args(1).buf));
                        ++index;
                        skip(spacesAndNewLines);
                        if getCurrentContext(contextStack).indented {
                            printIndent(indent);
                        } else {
                            printIndent(indent + 4);
                        }
                    } else {
                        printSourceCode(String.format("%s ", args(1).buf));
                    }
                }),
                // Unary Operators
                Rule("(--|\\+\\+)[ \t]*", closure(args: Array[String]) {
                    if getCurrentContext(contextStack) == "comment" printSourceCode(args(0))
                    else printSourceCode(args(1));
                }),
                // 2 Char Operators
                Rule("(==|!=|<<|>>|&&|\\|\\|)[ \t]*", closure(args: Array[String]) {
                    if getCurrentContext(contextStack) == "comment" printSourceCode(args(0))
                    else printSourceCode(String.format(" %s ", args(1).buf));
                }),
                // Single Char Operators
                Rule("([-+*/=&|$<>])[ \t]*", closure(args: Array[String]) {
                    if getCurrentContext(contextStack) == "comment" printSourceCode(args(0))
                    else printSourceCode(String.format(" %s ", args(1).buf));
                }),
                // Block Statements
                Rule(
                    String.format("(module|class|function|handler|دالة|وحدة|عملية)%s", nonIdentifierChar.buf),
                    closure(args: Array[String]) {
                        if getCurrentContext(contextStack) == "comment" printSourceCode(args(0))
                        else {
                            moveBackUtf8Chars(1);
                            setCurrentContext(contextStack, "block");
                            printSourceCode(String.format("%s ", args(1).buf));
                        }
                    }
                ),
                // If/While Statements
                Rule(String.format("(if|while|for|إذا|بينما|لكل)%s", nonIdentifierChar.buf), closure(args: Array[String]) {
                    if getCurrentContext(contextStack) == "comment" printSourceCode(args(0))
                    else {
                        moveBackUtf8Chars(1);
                        setCurrentContext(contextStack, "if");
                        printSourceCode(String.format("%s ", args(1).buf));
                    }
                }),
                // New Lines
                Rule("\n", closure(args: Array[String]) {
                    if getCurrentContext(contextStack) == "comment" {
                        printSourceCode("\n");
                    } else if (
                        getCurrentContext(contextStack) != "block" &&
                        getCurrentContext(contextStack) != "if"
                    ) or (
                        !isNext(spacesAndNewLinesThenOpeningBrace)
                    ) {
                        if isNext(getCurrentContext(contextStack).closingMatcher) {
                            skip(spacesAndNewLines);
                        } else {
                            printSourceCode("\n");
                            def result: Array[String] = nonEmptyLine.match(getCurrentContent());
                            if result.getLength() == 0 handleNewLines(contextStack, 0)
                            else if isNext(spacesAndNewLinesThenElseKeyword) {
                                printIndent(indent);
                                skip(spacesAndNewLines);
                            } else {
                                printIndent(indent + 4);
                            }
                        }
                    }
                }),
                // Semicolon
                Rule("(;|؛)", closure(args: Array[String]) {
                    if getCurrentContext(contextStack) == "comment" printSourceCode(args(0))
                    else {
                        setCurrentContext(contextStack, "");
                        printSourceCode(args(0));
                        handleNewLines(contextStack, 1);
                    }
                }),
                // Parenthesis
                Rule("(\\[|\\()", closure(args: Array[String]) {
                    printSourceCode(args(0));
                    if getCurrentContext(contextStack) != "comment" {
                        if isNext(spacesAndNewLinesThenClosingParen) {
                            skip(spacesAndNewLines);
                            contextStack.add(Context(String(), false, spacesAndNewLinesThenClosingParen));
                        } else if isNext(nonEmptyLine) {
                            contextStack.add(Context(String(), false, spacesAndNewLinesThenClosingParen));
                        } else {
                            skip(spacesAndNewLines);
                            indent += 4;
                            handleNewLines(contextStack, 1);
                            contextStack.add(Context(String(), true, spacesAndNewLinesThenClosingParen));
                        }
                    }
                }),
                Rule("(\\]|\\))", closure(args: Array[String]) {
                    if getCurrentContext(contextStack) == "comment" printSourceCode(args(0))
                    else {
                        if getCurrentContext(contextStack).indented {
                            printSourceCode("\n");
                            indent -= 4;
                            printIndent();
                        }
                        printSourceCode(args(0));
                        popContextStack(contextStack);
                        if isNext(spacesThenIdentifier) {
                            printSourceCode(" ");
                        }
                    }
                }),
                // Braces
                Rule("\\{", closure(args: Array[String]) {
                    if getCurrentContext(contextStack) == "comment" printSourceCode(args(0))
                    else {
                        def forceBlock: Bool = getCurrentContext(contextStack) == "block" ||
                            getCurrentContext(contextStack) == "if";

                        if forceBlock printSourceCode(" ");
                        printSourceCode("{");

                        if not forceBlock and isNext(spacesThenClosingBrace) {
                            contextStack.add(Context(String(), false, spacesAndNewLinesThenClosingBrace));
                        } else if not forceBlock and isNext(closingBraceOnSameLine) {
                            printSourceCode(" ");
                            contextStack.add(Context(String(), false, spacesAndNewLinesThenClosingBrace));
                        } else if isNext(spacesAndNewLinesThenClosingBrace) {
                            skip(spacesAndNewLines);
                            indent += 4;
                            handleNewLines(contextStack, 0);
                            contextStack.add(Context(String(), true, spacesAndNewLinesThenClosingBrace));
                        } else {
                            skip(spacesAndNewLines);
                            indent += 4;
                            handleNewLines(contextStack, 1);
                            contextStack.add(Context(String(), true, spacesAndNewLinesThenClosingBrace));
                        }
                    }
                }),
                Rule("\\}", closure(args: Array[String]) {
                    if getCurrentContext(contextStack) == "comment" printSourceCode(args(0))
                    else {
                        def indented: Bool = getCurrentContext(contextStack).indented;
                        if indented {
                            printSourceCode("\n");
                            indent -= 4;
                            printIndent();
                        } else {
                            printSourceCode(" ");
                        }
                        printSourceCode("}");
                        popContextStack(contextStack);
                        if isNext(spacesAndNewLinesThenElseKeyword) {
                            printSourceCode(" ");
                            skip(spacesAndNewLines);
                        } else if isNext(spacesAndNewLinesThenSemiColon) {
                            if getCurrentContext(contextStack) == "block" {
                                skip(spacesAndNewLinesThenSemiColon);
                                handleNewLines(contextStack, 2);
                            } else {
                                skip(spacesAndNewLines);
                            }
                        } else if indented {
                            if getCurrentContext(contextStack) == "block" {
                                handleNewLines(contextStack, 2);
                            } else {
                                handleNewLines(contextStack, 1);
                            }
                        }
                    }
                }),
                // Consecutive Words
                Rule(String.format("(%s+)[ \t]+%s", identifierChar.buf, identifierChar.buf), closure(args: Array[String]) {
                    if getCurrentContext(contextStack) == "comment" printSourceCode(args(0))
                    else {
                        printSourceCode(String.format("%s ", args(1).buf));
                        moveBackUtf8Chars(1);
                    }
                }),
                // Spaces
                Rule("[ \t]+", closure(args: Array[String]) {
                    if getCurrentContext(contextStack) == "comment" printSourceCode(args(0))
                }),
            });
        }

        function popContextStack(stack: ref[Array[Context]]) {
            if stack.getLength() > 1 {
                stack.remove(stack.getLength() - 1);
            }
        }

        function setCurrentContext(stack: ref[Array[Context]], val: CharsPtr) {
            stack(stack.getLength() - 1) = val;
        }

        function getCurrentContext(stack: ref[Array[Context]]): ref[Context] {
            return stack(stack.getLength() - 1);
        }

        function handleNewLines (stack: ref[Array[Context]], minNewLines: Int) {
            if isNext(getCurrentContext(stack).closingMatcher) {
                skip(spacesAndNewLines);
            } else {
                def match: Array[String] = spacesAndNewLines.match(getCurrentContent());
                def indentNeeded: Bool = false;
                if match.getLength() > 0 {
                    def i: Int;
                    for i = 0, i < match(0).getLength(), ++i {
                        if match(0)(i) == '\n' {
                            printSourceCode("\n");
                            indentNeeded = true;
                            --minNewLines;
                        }
                    }
                    index += match(0).getLength();
                }
                while (minNewLines > 0) {
                    printSourceCode("\n");
                    indentNeeded = true;
                    --minNewLines;
                }
                if indentNeeded printIndent();
            }
        }

        class Context {
            def context: String;
            def indented: Bool = false;
            def closingMatcher: ref[Regex.Matcher];
            closingMatcher~ptr = 0;

            handler this~init() {}
            handler this~init(c: String, i: Bool) {
                this.context = c;
                this.indented = i;
            }
            handler this~init(c: String, i: Bool, cm: ref[Regex.Matcher]) {
                this.context = c;
                this.indented = i;
                this.closingMatcher~no_deref = cm;
            }
            handler this~init(ref[this_type]) {
                this = value;
            }
            handler this = ref[this_type] {
                this.context = value.context;
                this.indented = value.indented;
                this.closingMatcher~no_deref = value.closingMatcher;
            }
            handler this = String {
                this.context = value;
            }
            handler this = CharsPtr {
                this.context = value;
            }
            handler this == CharsPtr {
                return this.context == value;
            }
            handler this != CharsPtr {
                return this.context != value;
            }
        }
    }
}
